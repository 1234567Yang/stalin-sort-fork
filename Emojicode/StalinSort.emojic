📗
	A class for sorting arrays.
📗
🌍 🐇 🧙‍♂️ 🐚T⚪🍆 🍇
	🖍🆕 comparator 🍇 T T ➡ 🔢 🍉

	🆕 🍼 comparator 🍇 T T ➡ 🔢 🍉 🍇🍉

	📗
		Returns a sorted copy of the given array.
	📗
	❗ 🧙‍♀️ unsortedArray 🍨🐚T🍆 ➡ 🍨🐚T🍆 🍇
		💭 Initializes an array of the same size of the `unsortedArray`
		🆕 🍨 🐚T🍆 🐴 🐔 unsortedArray ❗ ❗ ➡ 🖍🆕 sortedArray

		🐽 unsortedArray 0 ❗ ➡ 🖍🆕 previous

		🔂 item unsortedArray 🍇
			💭 Adds all items that's greater or equal to the previously added item
			↪ ⁉ comparator item previous ❗ ▶🙌 0 🍇
				🐻 sortedArray item ❗
				item ➡ 🖍 previous
			🍉
		🍉

		↩️ sortedArray
	🍉
🍉

🏁 🍇
	💭 Creates an unsorted array
	🍨 1 2 5 4 6 6 8 7 🍆 ➡ array

	💭 Creates a local comparator function
	🍇 🎍🥡 a 🔢 b 🔢 ➡ 🔢
		↩ a ➖ b
	🍉 ➡ numberComparator

	💭 Creates a StalinSorter that sorts numbers
	🆕 🧙‍♂️ 🐚🔢🍆 🆕 numberComparator ❗ ➡ sorter

	💭 Sorts the array with the sorter
	🧙‍♀️ sorter array ❗ ➡ sortedArray

	💭 Prints all the elements of the array
	🔂 item sortedArray 🍇
		😀 🔡 item 10 ❗ ❗
	🍉
🍉